#!/usr/bin/perl

use threads;
use Tabix;
use strict;
use warnings;
use Math::CDF;
use Set::IntervalTree;
use Set::IntSpan::Fast;
use Getopt::Long;
no autovivification;

my $usage = "

NO-CALL-FILTER --bed my.bedfile --build my.build.txt -c coverage.gz -g gvf  -l low.coverage.cut -q low.quality.cut --threads 5

";

my ($help);
my $bgi_depth;
my $gvf;
my $build;
my $quality = 20;
my $ldepth = 5;
my $hdepth;
my $coverage;
my $bed;
my $n_threads = 4;
my $opt_success = GetOptions('help'         => \$help,
                             "low_depth=s"  => \$ldepth,
			     "high_depth=s" => \$hdepth,
			     "coverage=s"   => \$coverage,
			     "gvf=s"        => \$gvf, 
			     "quality=s"    => \$quality,
                             "build=s"      => \$build,
			     "bed=s"        => \$bed,
			     "threads=s"    => \$n_threads,
    );

die $usage if $help || ! $opt_success;
die $usage unless defined $gvf && defined $ldepth && defined $quality && defined $build && defined $coverage && $bed; 

my %BUILD;
my %BED;

#-----------------------------------------------------------------------------
#----------------------------------- MAIN ------------------------------------
#-----------------------------------------------------------------------------

Parse_Build($build);
Parse_BED($bed);
my $gvf_t    = Tabix->new(-data=>$gvf);
my %features = map {$_ => 1}  $gvf_t->getnames();
Run_Threads(\%features);

#-----------------------------------------------------------------------------
#-------------------------------- SUBROUTINES --------------------------------
#-----------------------------------------------------------------------------
sub Handel_Run{
    my $feature             = shift;
    my $gvf_t               = Tabix->new(-data=>$gvf);
    my $cov_t               = Tabix->new(-data=>$coverage);
    my $gvf_iter            = $gvf_t->query($feature);
    my $cov_iter            = $cov_t->query($feature);
    my $depth_count  = Count_Depth($cov_t, $cov_iter);
    $hdepth                 = Estimate_High($depth_count, $feature) if ! defined $hdepth;
    $cov_iter               = $cov_t->query($feature);
    my ($lranges, $hranges) = Range_Depth($cov_t, $cov_iter, $feature);
    my $gvf_ranged          = Process_GVF($gvf_t, $gvf_iter, $lranges, $hranges, $feature);
    Print_Gvf($feature, $gvf_ranged);
}
#-----------------------------------------------------------------------------
sub Run_Threads{
    my $features = shift;
    my @features = keys %{$features};
    
    while(@features){
	my @child;
	my @temp_features;
	if (scalar @features % $n_threads){
	    for(my $i = 0; $i <= $n_threads; $i++){
		my $feature = shift @features;
		push @temp_features, $feature;
	    }
	}
	else{
	    for(my $i = 0; $i <= scalar @features; $i++){
		my $feature = shift @features;
		push @temp_features, $feature;
	    }
	}
	my $child = Fork_Threads(\@temp_features);
	foreach my $c (@{$child}){
	    my $tmp = waitpid($c, 0);
	}
    }
}

#-----------------------------------------------------------------------------
sub Fork_Threads{    
    my $f = shift;
    my @childs;
    foreach my $feature (@{$f}){ 
	next if ! defined $feature;
	my $pid = fork();
        if ($pid) {
	    # parent
	    #print "pid is $pid, parent $$\n";
	    push(@childs, $pid);
        } elsif ($pid == 0) {
	    # child
	    Handel_Run($feature);
	    exit 0;
        } else {
	    die "couldnt fork: $!\n";
        }
    }	
    return \@childs;
}

#-----------------------------------------------------------------------------

sub Parse_BED{
    my $file = shift;
    open(FH, '<', $file) || die "cannot open BED $file\n";
    while (my $l = <FH>) {
	chomp $l;
	
	# Model-centric match.  I should consider changing this. 
	
	next if $l !~ /^chr*/;
	my @l = split /\t/,  $l;
	$BED{$l[0]} += ($l[2] - $l[1]) + 1;
    }
    close FH;
}
#-----------------------------------------------------------------------------
    
# In order to add a gap from the beginning of the genome to the first exome
# regions you need to parse the build so that you know from 0 -> start and  
# 999 -> end of each seqid.  data is $BUILD{seqid} = length;

sub Parse_Build{
    my $file = shift;
    open(FH, '<', $file) || die "cannot open build $file\n";
    while(my $l = <FH>){
	
	# skipping lines that begin with whitespace or comments

	next if $l =~ /^\s+|#/;
	my @l = split /\s+/, $l;
	die "$file is not in correct format\n" if scalar @l != 3;
	$BUILD{$l[0]} = $l[2];
    }
    close FH;
}
#-----------------------------------------------------------------------------                        

# I decided to bin the counts for each depth in a hash incase I wanted to
# change the way I used the depth count information. $depth_count{depth=10}++
# Returned is the depth count hash.

sub Count_Depth{ 
    my ($t,  $iter) = @_;
    my %depth_count;
    print STDERR "Current Stage: Counting depth\n";
    while(my $l = $t->read($iter)){
	my @l = split /\t/, $l;
	$depth_count{$l[2]}++; 
    }
    return \%depth_count;
}  
#-----------------------------------------------------------------------------

# Loop through %depth_count and sum the depths across all counts.
# lambda (mean) is the $total_depth / feature length.  whole genome needs a
# a bed file which will  be the same as build. I am looking at all position in
# the bed this means the lambda will be lower than if i only look at positions
# that have coverage. The high depth cutoff is returned.

sub Estimate_High{
    my ($depth_count, $feature) = @_;
    print STDERR "Current Stage: Estimating upper depth cutoff\n";
    my $total_depth;
    while(my ($depth, $count) = each %{$depth_count}){
	$total_depth  += $depth * ($count + 1);
    }
    my $lambda  = $total_depth > 0 ? $total_depth / $BED{$feature} : 0;
    print STDERR "The lamda for $feature is $lambda\n";
    my $h_cut   = $lambda == 0 || ! defined $lambda ? 0 : Math::CDF::qpois(0.98, $lambda);
    print STDERR "low coverage dected! check $feature. high coverage cutoff \= $h_cut\n" if $h_cut < 1;
    return $h_cut;
}      
#-----------------------------------------------------------------------------

# I am handeling all gap infromation and ranges in Set::IntSpan::Fast.  
# Critical to have the compiled version of Set::IntSpan::Fast
# Returns the Set::IntSpan::Fast::Object

sub Range_Depth{
    my ($t, $iter, $feature) = @_;
   
    # Initializing the set
     
    my $lset = Set::IntSpan::Fast->new();   
    my $hset = Set::IntSpan::Fast->new();
    # Last seen position

    my $lpos = 0;

    print STDERR "Current Stage: Ranging gap\n";
    while(my $l = $t->read($iter)){
	my @l = split /\t/, $l;
	
	# Adds gaps for regions where there is a break 
	# in ther coordinates

	if($l[1] != $lpos + 1){
	    my $start = $lpos+1;
	    my $end = $l[1]-1;
	    $lset->add_range($start, $end);
	}
	
	# Reassigning last position

	$lpos = $l[1];

	# If a position fails on depth high or low pass to 
	# gap set

	$lset->add($l[1]) if $l[2] < $ldepth;
	$hset->add($l[1]) if $l[2] > $hdepth;
    }

    # Check at end to see if we are to the end of the length of the seqid
    # if not pass the range as a gap to the set

    if($lpos < $BUILD{$feature}){
	my $start = $lpos + 1;
	my $end = $BUILD{$feature};
	$lset->add_range($start, $end); 
    }

    return $lset, $hset;
}	
#-----------------------------------------------------------------------------
sub Process_GVF{

    # Create the GVF hash, add gaps, and parse the current feature.
    # Iterate the Set object to add gaps the the GVF hash.
  
    my ($t, $iter, $lranges, $hranges, $feature) = @_;
    my %gvf;

    #Loop the GVF;
    
    while(my $l = $t->read($iter)){
	my ($seqid, $source, $type, $start, $end, $score, $strand, $phase, 
	    $attributes) = split /\t/, $l;
	
	# Contains_any makes sure the SNV/INDEL ect doesn't overlap a high or low gap.
	
	if(! defined $lranges->contains_any($start .. $end) && !defined $hranges->contains_any($start .. $end)){
	    $gvf{$start}{$end} = "$type\t$source\t$score\t$strand\t$phase\t$attributes" 
	    }
	
	# If contains any is true we want to process genotypes contained in 
	# attriubte line.
	
	else{
	    
	    #Parse the Attributes.
	    
	    my %attributes;
	    foreach my $key_val_pair (split /;/, $attributes){
		my ($key, $value) = split /=/, $key_val_pair;
		die "Attribute line is not conform to spefication:\n$l\n" if ! defined $key || ! defined $value;
		$attributes{$key} = $value;
	    }
	    
            if($score < $quality){
		$attributes{Zygosity} = '.';
                $attributes{Variant_seq} = '^:^';
		$attributes{Nocall} = 'low_quality';
	    }
	    
	    $attributes{Zygosity} = '.';
	    
	    #checking gaps
	    
	    if (defined $lranges->contains_any($start .. $end)){
		
		$lranges->remove_range($start, $end);
		
		# if the variant line is longer than three we know its a het or some strange polymorphic
		# site within an indvidual.  We are assuming these sites shouldn't be touched?  Chad's thoughts. 
		
		my @genotype = grep {!/$attributes{Reference_seq}/} split /,/, $var;
		if(scalar @genotype > 1){
		    @genotype = ('^','^');
		}
		else{
		    push @genotype,'^';
		}
		$attributes{Variant_seq} = join ",", @genotype;	    
		$attributes{Nocall} = 'low_coverage';
	    }
	    
	    #high depth does not get called hemizygous.
	    
	    else{    
		
		$hranges->remove_range($start, $end);
		$attributes{Zygosity} = '.';
		$attributes{Variant_seq} = '^:^';	
		$attributes{Nocall} = 'high_coverage';
	    }
	    
	    my @attrib;
	    my @order = ("ID","Variant_seq", "Reference_seq", "Total_reads", "Zygosity");
	    foreach my $o (@order){
		push @attrib, join "=", ($o, $attributes{$o});
		delete $attributes{$o};
	    }
	    
	    while(my($k, $v) = each %attributes){
		push @attrib, join "=", ($k,  $v);
	    }
	    
	    $attributes = join ";", @attrib;	
	    $gvf{$start}{$end} = "$type\t$source\t$score\t$strand\t$phase\t$attributes;";
	    
	}  
    }
    
    print STDERR "Current Stage: adding gaps\n";
    my $it = $lranges->iterate_runs();
    while (my ( $from, $to ) = $it->()) {
	$gvf{$from}{$to} = "gap\tno_call\t.\t.\t.\tID=$feature:$from:$to:low_depth;";
    }
    
    my $it2 = $hranges->iterate_runs();
    while (my ( $from, $to ) = $it2->()) {
        $gvf{$from}{$to} = "gap\tno_call\t.\t.\t.\tID=$feature:$from:$to:high_depth;";
    }
    
    return(\%gvf); 
}
#-----------------------------------------------------------------------------

sub Print_Gvf{
    
    my $feature = shift;
    my $gvf     = shift;
    
  OUTER: while(my($s, $e_hash) = each %{$gvf}){
    INNER: while(my($e, $d) = each %{$gvf->{$s}}){
	my ($type, $source, $score, $strand, $phase, $attributes) = split /\t/, $d;
	print "$feature\t$source\t$type\t$s\t$e\t$score\t$strand\t$phase\t$attributes\n"   
	}
  }
}
#-----------------------------------------------------------------------------



